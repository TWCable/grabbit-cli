buildscript {
    repositories {
        maven {
            url "http://dl.bintray.com/jmoore/java-lib"
        }
    }
    //noinspection GroovyAssignabilityCheck
    dependencies {
        classpath 'com.mooregreatsoftware:gradle-defaults:1.0.6'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.4'
    id "us.kirchmeier.capsule" version "1.0.2"
}

group = 'com.twcable.grabbit'
description = 'Command line interface for interacting with the Grabbit Client'

apply plugin: 'groovy'

//noinspection GroovyAssignabilityCheck
dependencies {
    compile "org.codehaus.groovy:groovy-all:2.4.6"
    compile 'commons-cli:commons-cli:1.2'

    testCompile "org.spockframework:spock-core:1.0-groovy-2.4", {
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
}

apply plugin: 'com.mooregreatsoftware.defaults'

defaultTasks 'build'

defaults {
    compatibilityVersion = '1.7'

    id = 'twcable'

    orgName = 'Time Warner Cable'
    orgUrl = 'http://www.timewarnercable.com'

    bintrayRepo = 'aem'
    bintrayLabels = ['grabbit', 'aem']

    developers = [
        [id: 'jmoore', name: 'Jim Moore', email: 'moore.jim@gmail.com'],
    ]

    copyrightYears = '2014-2015'
}


task fatCapsule(type: FatCapsule) {
    archiveName = "grabbit-cli-${version}"

    applicationClass "com.twcable.grabbit.cli.Grabbit"

    capsuleManifest {
        minJavaVersion = defaults.compatibilityVersion
    }

    manifest = jar.manifest

    reallyExecutable
}

assemble.dependsOn fatCapsule


bintray {
    filesSpec {
        from fatCapsule.archivePath
        into '/bin'
    }

    pkg {
        licenses = ['Apache-2.0']
        attributes = ['plat': ['jvm', 'aem', 'grabbit', 'cq']]
    }
}

//noinspection GroovyAssignabilityCheck
wrapper {
    gradleVersion = 2.13
}
